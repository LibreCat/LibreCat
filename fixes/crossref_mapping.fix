# mapping data from CrossRef to the local repository

retain(message)
move(message, '')

add('r.status','new')

# look up publication type
move(type, r.type)
lookup(r.type, fixes/lookup/cr_type_mapping.csv, default: journal_article)

#title
move(title, r.title)
join(r.title,' ')
trim(r.title)

#year
move(issued.date-parts.0.0, r.year)
move(deposited.date-parts.0.0, r.year)
move(created.date-parts.0.0, r.year)
trim(r.year)

# identifier
move(DOI, r.doi)
move(ISSN, r.publication_identifier.issn)
move(ISBN, r.publication_identifier.isbn)
replace_all(r.publication_identifier.isbn.*, '^http://id.crossref.org/isbn/(.*)',$1)

# authors
move(author, r.author)
do list (path => r.author)
  do identity ()
    move(given, first_name)
    move(family, last_name)
    remove(affiliation)
  end
end

# editors
move(editor, r.editor)
do list (path => r.editor)
  do identity ()
    move(given, first_name)
    move(family, last_name)
    remove(affiliation)
  end
end

add(r.publication_status, published)

# type mapping
if all_match(r.type, 'journal_article')
    move(container-title.0, r.publication)
    if any_match(page, '-')
      move(page, r.page)
    else
      move(page, r.article_number)
    end
end

# book
if all_match(r.type, 'book')
    move(publisher-location, r.place)
    if exists(r.editor)
      set(r.type, book_editor)
    end
end

if all_match(r.type, 'book_chapter')
    move(container-title.0, r.publication)
    move(container-title.1, r.series_title)
end

if all_match(r.type, 'conference')
    move(container-title.0, r.publication)
end

if all_match(r.type, preprint)
# CHP registers DOIs for bioRxiv via Crossref, eg 10.1101/005538
  if all_match(member, '246')
    set(r.publication, bioRxiv)
  end
end

move(abstract, r.abstract)
if exists(r.abstract)
  unless is_array(r.abstract)
    move(r.abstract, r.abstract.0.text)
    add(r.abstract.0.lang, eng)
  end
end

# fix language and map to iso
move(language, r.language.0.iso)
lookup(r.language.0.iso, fixes/lookup/cr_lang_iso.csv, default: eng)

retain(r)
move(r, .)
