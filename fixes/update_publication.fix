# fix publication data at update

compact(department)
compact(project)
vacuum()
form2schema(publication_identifier)
form2schema(external_id)
page_range_number()
clean_preselects()
add_urn()
person()
volume_sort()
research_data_doi()

if exists(author)
  do list(path:author, var:loop)
    do identity()
      copy_field(loop.id,loop.tmp)
      lookup_in_store(loop.tmp, search, bag:user)
      move_field(loop.tmp.orcid, loop.orcid)
      remove_field(loop.tmp)
    end
  end
end

if exists(editor)
  do list(path:editor, var:loop)
    do identity()
      copy_field(loop.id,loop.tmp)
      lookup_in_store(loop.tmp, search, bag:user)
      move_field(loop.tmp.orcid, loop.orcid)
      remove_field(loop.tmp)
    end
  end
end

if exists(translator)
  do list(path:translator, var:loop)
    do identity()
      copy_field(loop.id,loop.tmp)
      lookup_in_store(loop.tmp, search, bag:user)
      move_field(loop.tmp.orcid, loop.orcid)
      remove_field(loop.tmp)
    end
  end
end

if exists(department)
  do list(path:department, var:loop)
    do identity()
      copy_field(loop._id,loop.tmp)
      lookup_in_store(loop.tmp, search, bag:department)
      move_field(loop.tmp.tree, loop.tree)
      remove_field(loop.tmp)
    end
  end
end

if exists(project)
  do list(path:project, var:loop)
    do identity()
      copy_field(loop._id,loop.tmp)
      lookup_in_store(loop.tmp, search, bag:project)
      move_field(loop.tmp.call_identifier,loop.call_identifier)
      move_field(loop.tmp.grant_number,loop.grant_number)
      remove_field(loop.tmp)
      if all_match(loop.call_identifier,'FP7|H2020')
        add_field(ec_funded,1)
      end
    end
  end
end

if all_match(external_id.isi.0, '^\w+$')
  add_field(isi, 1)
end
if all_match(external_id.pmid.0, '^.+$')
  add_field(pmid, 1)
end


split_field(nasc, ';|,')
trim(nasc.*)
split_field(genbank, ';|,')
trim(genbank.*)
split_field(keyword, ';|,')
trim(keyword.*)

if all_match(status,"returned|deleted")
  add_field(oai_deleted, 1)
else
  remove_field(oai_deleted)
end

# Force the year field to be a string (see #267)
string(year)

remove_field('idm')
remove_field('editor_idm')
remove_field('translator_idm')
remove_field('supervisor_idm')

vacuum()
